// Gradle build file

plugins {
	id 'groovy'
	id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'


version = "0.15"
group = 'org.dx42'


sourceSets {
	main {
		java { srcDirs = [] }
		groovy { srcDirs = ['src/main/java'] }
	}
	test {
		java { srcDirs = [] }
		groovy { srcDirs = ['src/test/java'] }
	}
}

repositories {
	maven { url "http://repo.maven.apache.org/maven2" }
	jcenter()
}

dependencies {
	compile "org.codehaus.groovy:groovy-all:2.3.3"
	compile 'org.slf4j:slf4j-api:1.7.25'
	
	testCompile 'ch.qos.logback:logback-classic:1.2.3'
	testCompile "org.codehaus.groovy:groovy-ant:2.3.3"
	
	testCompile 'junit:junit:4.12'
	testCompile 'log4j:log4j:1.2.17'
	
	
	testCompile('org.gradle:gradle-core:3.2')  {				// Because the Gradle API is used within some sample Gradle build.gradle files
		exclude group: 'org.gradle', module:'gradle-logging'	// To avoid "Class path contains multiple SLF4J bindings" warning
	}
	testCompile group:'org.codenarc', name:'CodeNarc', version: '1.2.1', transitive:false
	testCompile group:'org.gmetrics', name:'GMetrics', version: '1.0', transitive:false
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

//------------------------------------------------------------------------------
// Bintray
//------------------------------------------------------------------------------

bintray {
	user = bintrayUser
	key = bintrayApiKey
	publications = ['MyPublication']
	configurations = ['archives']
	publish = true
	override = false
	
	pkg {
		repo = 'dx42'
		name = 'needy'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/dx42/Needy.git'
		
		version {
			name = project.version
			desc = 'Needy jar ' + project.version
			released  = new Date()
			//vcsTag = project.version
			//attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
		}
	}
}

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			groupId project.group
			artifactId 'needy'
			version project.version
			
			artifact sourcesJar
		}
	}
}